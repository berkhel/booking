plugins {
    id 'buildlogic.java-application-conventions'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

application {
    mainClass = 'it.berkhel.email.Main'
}

dependencies {
    implementation 'org.simplejavamail:simple-java-mail:8.12.4'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
}



testing {
    suites {
        configureEach {
            targets {
                all {
                    testTask.configure {
                        jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
                    }
                }
            }
        }
        test {
            sources {
                java {
                    include 'it/berkhel/email/test/**'
                }
            }
        }
        functionalTest(JvmTestSuite) {
            dependencies{
                implementation project(':email')
                implementation 'org.springframework.boot:spring-boot-starter-test'
                implementation 'io.rest-assured:rest-assured:5.5.0'
                implementation 'org.testcontainers:junit-jupiter:1.20.4'
                implementation 'org.springframework.boot:spring-boot-testcontainers:3.4.1'
                implementation 'org.testcontainers:rabbitmq:1.20.4'
                implementation 'org.springframework.boot:spring-boot-starter-amqp'
                implementation 'org.awaitility:awaitility:4.2.2'
                implementation 'com.rabbitmq:amqp-client:5.24.0'
                implementation 'com.icegreen:greenmail:2.1.2'
                implementation 'com.icegreen:greenmail-junit5:2.1.2'
            }
            sources {
                java {
                    srcDirs += ['src/test/java']
                    include 'it/berkhel/email/functional/**'
                }
            }
        }
    }
}