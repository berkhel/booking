plugins {
    id 'buildlogic.java-application-conventions'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
}


application {
    mainClass = 'it.berkhel.booking.SpringBootMain'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
}


testing {
    suites {
        configureEach {
            targets {
                all {
                    testTask.configure {
                        jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
                    }
                }
            }
        }
        test {
            sources {
                java {
                    include 'it/berkhel/booking/unit/**'
                }
            }
        }
        functionalTest(JvmTestSuite) {
            dependencies {
                implementation project(':app')
                implementation 'org.springframework.boot:spring-boot-starter-test'
                implementation 'io.rest-assured:rest-assured:5.5.0'
                implementation 'org.hamcrest:hamcrest:3.0'
            }
            sources { 
                java { 
                    srcDirs += ['src/test/java']
                    include 'it/berkhel/booking/functional/**'
                }
            }
        }
    }
}