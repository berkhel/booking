plugins {
    id 'buildlogic.java-common-conventions'
    id 'jvm-test-suite' 
    id 'jacoco'
    id 'jacoco-report-aggregation'
}

configurations {
    mockitoAgent // see https://javadoc.io/doc/org.mockito/mockito-core/latest/org/mockito/Mockito.html#0.3
}


dependencies{
    // Use JUnit Jupiter for testing
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Use Mockito for mocking
    mockitoAgent(libs.mockito) {
        transitive = false
    }
    testImplementation libs.mockito.junit.jupiter

    // Use Hamcrest for assertions
    testImplementation 'org.hamcrest:hamcrest:3.0'

}


testing{
    suites{
        configureEach {
            useJUnitJupiter()
            targets {
                all {
                    testTask.configure {
                        testLogging { 
                            events = [
                               "passed",
                               "failed",
                                "skipped"
                            ]
                            exceptionFormat = "full" 
                            showCauses = true 
                            showExceptions = true 
                            showStackTraces = true 
                            showStandardStreams = true 
                        }
                    }
                }
            }
        }
    }
}



jacocoTestCoverageVerification {
    dependsOn test
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }
    }
}

testCodeCoverageReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        html.outputLocation = rootProject.layout.buildDirectory.dir("reports/jacoco/testCodeCoverageReport")
    }
}